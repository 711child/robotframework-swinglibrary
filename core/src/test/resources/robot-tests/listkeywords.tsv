*Setting*	*Value*
library	SwingLibrary
library	Collections
testSetup	clearSelectionFromList	${listIndex}

*Variable*	*Value*
${listName}	testList
${listWithRendererName}	listWithRenderer
${listIndex}	0
${listValues}	one	two	three	

*Test Case*	*Action*	*Argument*
Get Selected List Value From List By Index
	selectFromList	${listIndex}	0
	${selectedValue}=	getSelectedValueFromList	0
	shouldBeEqual	${selectedValue}	one

Get Selected List Value From List By Name
	selectFromList	${listName}	0
	${selectedValue}=	getSelectedValueFromList	${listName}
	shouldBeEqual	${selectedValue}	one

Get Selected List Value From Custom Rendered List By Name
	selectFromList	${listWithRendererName}	0
	${selectedValue}=	getSelectedValueFromList	${listWithRendererName}
	shouldBeEqual	${selectedValue}	ONE
	selectFromList	${listWithRendererName}	TWO
	${selectedValue}=	getSelectedValueFromList	${listWithRendererName}
	shouldBeEqual	${selectedValue}	TWO

Select From List By Index
	selectFromList	${listIndex}	0
	listSelectionShouldBe	${listIndex}	one
	selectFromList	${listIndex}	1
	listSelectionShouldBe	${listIndex}	two

Select From List By Name
	selectFromList	${listName}	one
	listSelectionShouldBe	${listName}	one
	selectFromList	${listName}	three
	listSelectionShouldBe	${listName}	three

Select From List By Name Not Found
	#${oldSetting}=	Set Jemmy Timeout	JListOperator.WaitFindItemIndexTimeout	3
	#${start}=	Get Time	epoch
	${err}=	Run Keyword And Expect Error	*	selectFromList	${listName}	horse
	#${end}=	Get Time	epoch
	#Should Be True	${end} - ${start} >= 3
	#Should Start With	${err}	Couldn't find item:
	#Set Jemmy Timeout	JListOperator.WaitFindItemIndexTimeout	${oldSetting}

Select Multiple Items From List
	selectFromList	${listName}	one	two	three
	allListItemsShouldBeSelected

Selecting Item By A Substring Of The Name Should Fail
	runKeywordAndExpectError	*	selectFromList	${listIndex}	hre

Click On List Item
	clickOnListItem	${listName}	three	2
	listSelectionShouldBe	${listName}	three
	${clickCount}=	getSelectionClickCount
	shouldBeEqualAsIntegers	2	${clickCount}
	
Select All List Items
	selectAllListItems	${listName}
	allListItemsShouldBeSelected

Clear Selection From List
	selectFromList	${listIndex}	three
	listSelectionShouldBe	${listIndex}	three
	clearSelectionFromList	${listIndex}
	listSelectionShouldNotBe	${listIndex}	three

Get List Item Count By Index
	${itemCount}=	getListItemCount	${listIndex}
	shouldBeEqualAsIntegers	3	${itemCount}

Get List Item Count By Name
	${itemCount}=	getListItemCount	${listName}
	shouldBeEqualAsIntegers	3	${itemCount}

List Keywords Should Fail If Context Is Not A Window
	[setup]	selectEmptyContext
	runKeywordAndExpectError	*To use this keyword you must first select a correct context*	selectFromList	0	0
	runKeywordAndExpectError	*To use this keyword you must first select a correct context*	getSelectedValueFromList	0
	runKeywordAndExpectError	*To use this keyword you must first select a correct context*	clearSelectionFromList	0
	runKeywordAndExpectError	*To use this keyword you must first select a correct context*	getListItemCount	0
	[teardown]	selectMainWindow

*Keyword*	*Action*
listSelectionShouldBe	[arguments]	${listIndex}	${expectedValue}
	${selectedValue}=	getSelectedValueFromList	${listIndex}
	shouldBeEqual	${expectedValue}	${selectedValue}

listSelectionShouldNotBe	[arguments]	${listIndex}	${expectedValue}
	${selectedValue}=	getSelectedValueFromList	${listIndex}
	shouldNotBeEqual	${expectedValue}	${selectedValue}

allListItemsShouldBeSelected
	${selectedValues}=	getSelectedValues	0
	listsShouldBeEqual	${listValues}	${selectedValues}
