*Setting*	*Value*	*Value*
library	SwingLibrary
testSetup	clearTable

*Variable*	*Value*	*Value*
${tableName}	testTable
${columnHeaderOne}	column one
${enabledMenuItem}	Replace text
${disabledMenuItem}	Disabled menuitem

*Test Case*	*Action*	*Argument*	*Argument*
Table Cell Should Not Be Selected By Index
	tableCellShouldNotBeSelected	${tableName}	0	0

Table Cell Should Not Be Selected By Column Header
	tableCellShouldNotBeSelected	${tableName}	0	${columnHeaderOne}

Table Cell Should Be Selected By Index
	selectTableCell	${tableName}	3	2
	tableCellShouldBeSelected	${tableName}	3	2

Table Cell Should Be Selected By Column Header
	selectTableCell	${tableName}	3	column two
	tableCellShouldBeSelected	${tableName}	3	column two

Select Table Cell By Index
	selectTableCell	${tableName}	3	2
	${cellValue}=	getSelectedTableCellValue	${tableName}
	shouldBeEqual	four/three	${cellValue}

Select Table Cell By Column Header Name
	selectTableCell	${tableName}	1	column three
	${cellValue}=	getSelectedTableCellValue	${tableName}
	shouldBeEqual	two/three	${cellValue}

Select Table Cell Should Fail If The Cell Doesn't Exist
	runKeywordAndExpectError	The specified table cell (row: 10, column: 15) is invalid.	selectTableCell	${tableName}	10	15
	runKeywordAndExpectError	The specified table cell (row: 10, column: nonexisting) is invalid.	selectTableCell	${tableName}	10	nonexisting

Clear Table Selection
	selectTableCell	${tableName}	1	2
	tableCellShouldBeSelected	${tableName}	1	2
	clearTableSelection	${tableName}
	tableCellShouldNotBeSelected	${tableName}	1	2

Get Selected Table Cell Value
	selectTableCell	${tableName}	3	2
	${cellValue}=	getSelectedTableCellValue	${tableName}
	shouldBeEqual	four/three	${cellValue}

Get Table Cell Value By Index
	${cellValue}=	getTableCellValue	${tableName}	2	1
	shouldBeEqual	three/two	${cellValue}

Clear Table Cell
	clearTableCell	${tableName}	2	1
	${cellValue}=	getTableCellValue	${tableName}	2	1
	shouldBeEmpty	${cellValue}

Get Table Cell Value By Column Header
	${cellValue}=	getTableCellValue	${tableName}	1	column two
	shouldBeEqual	two/two	${cellValue}

Type Into Table Cell
	typeIntoTableCell	${tableName}	2	1	newValue
	${cellValue}=	getTableCellValue	${tableName}	2	1
	shouldBeEqual	newValue	${cellValue}

Set Table Cell Value
	setTableCellValue	${tableName}	1	0	four/one
	${cellValue}=	getTableCellValue	${tableName}	1	0
	shouldBeEqual	four/one	${cellValue}

Get Table Column Count By Name
	${columnCount}=	getTableColumnCount	${tableName}
	shouldBeEqualAsIntegers	4	${columnCount}

Get Table Column Count By Index
	${columnCount}=	getTableColumnCount	0
	shouldBeEqualAsIntegers	4	${columnCount}

Get Table Row Count By Name
	${rowCount}=	getTableRowCount	${tableName}
	shouldBeEqualAsIntegers	4	${rowCount}

Find Table Row
	${row}=	findTableRow	${tableName}	one/one	
	shouldBeEqualAsIntegers	0	${row}
	${row}=	findTableRow	${tableName}	three/two
	shouldBeEqualAsIntegers	2	${row}

Get Table Row Count By Index
	${rowCount}=	getTableRowCount	0
	shouldBeEqualAsIntegers	4	${rowCount}

Select From Table Cell Popup Menu
	selectFromTableCellPopupMenu	${tableName}	2	column one	Submenu|Enabled menuitem
	selectFromTableCellPopupMenu	${tableName}	2	1	Replace text
	${newCellValue}=	getTableCellValue	${tableName}	2	1
	shouldBeEqual	newValue	${newCellValue}

Select From Table Cell Popup Menu Accepts Integer Arguments
	${rowAsInteger}=	convertToInteger	3
	selectFromTableCellPopupMenu	${tableName}	${rowAsInteger}	1	Replace text
	${newCellValue}=	getTableCellValue	${tableName}	${rowAsInteger}	1
	shouldBeEqual	newValue	${newCellValue}

Table Cell Popup Menu Should Be Disabled
	tableCellPopupMenuShouldBeDisabled	${tableName}	2	column one	${disabledMenuItem}
	runKeywordAndExpectError	Menuitem '${enabledMenuItem}' at '2, column one' is enabled.	tableCellPopupMenuShouldBeDisabled	${tableName}	2	column one	${enabledMenuItem}

Table Cell Popup Menu Should Be Enabled
	tableCellPopupMenuShouldBeEnabled	${tableName}	2	column one	${enabledMenuItem}
	runKeywordAndExpectError	Menuitem '${disabledMenuItem}' at '2, column one' is disabled.	tableCellPopupMenuShouldBeEnabled	${tableName}	2	column one	${disabledMenuItem}

Table Keywords Fail If Context Is Not Set
	selectEmptyContext
	tableKeywordShouldFail	getTableCellValue	someTable	0	0
	tableKeywordShouldFail	getSelectedTableCellValue	someTable
	tableKeywordShouldFail	tableCellShouldBeSelected	someTable	0	0
	tableKeywordShouldFail	tableCellShouldNotBeSelected	someTable	0	0
	tableKeywordShouldFail	selectTableCell	someTable	0	0
	tableKeywordShouldFail	clearTableSelection	someTable
	tableKeywordShouldFail	getTableColumnCount	someTable
	tableKeywordShouldFail	getTableRowCount	someTable
	[teardown]	selectMainWindow

*Keyword*	*Action*	*Argument*	*Argument*	*Argument*	*Argument*	*Argument*	*Argument*
tableKeywordShouldFail	[Arguments]	${keyword}	@{arguments}
	runKeywordAndExpectError	*To use this keyword you must first select a correct context*	${keyword}	@{arguments}

clearTable
	selectMainWindow
	clearTableSelection	${tableName}
