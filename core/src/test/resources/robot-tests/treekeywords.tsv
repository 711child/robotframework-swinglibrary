*Setting*	*Value*
library	SwingLibrary
suiteSetup	selectMainWindow

*Variable*	*Value*	*Value*
${treeName}	testTree
${nodeIndex}	2
${rootNode}	The Java Series
${childNode1}	Books for Java Implementers
${childNode2}	Books for Java Programmers
${leafNode}	The Java Tutorial: A Short Course on the Basics
${anotherNodePath}	${rootNode}|${childNode2}
${leafNodePath}	${anotherNodePath}|${leafNode}

*Test Case*	*Action*	*Argument*	*Argument*
Expand Node by Index
	expandTreeNode	${treeName}	0
	expandTreeNode	${treeName}	${nodeIndex}
	treeNodeShouldBeExpanded	${treeName}	${nodeIndex}
	collapseTreeNode	${treeName}	${nodeIndex}
	collapseTreeNode	${treeName}	0

Expand Node By Name
	expandTreeNode	${treeName}	${rootNode}
	expandTreeNode	${treeName}	${anotherNodePath}
	treeNodeShouldBeExpanded	${treeName}	${anotherNodePath}
	collapseTreeNode	${treeName}	${anotherNodePath}
	collapseTreeNode	${treeName}	${rootNode}

Collapse Node By Index
	expandTreeNode	${treeName}	0
	expandTreeNode	${treeName}	${nodeIndex}
	treeNodeShouldBeExpanded	${treeName}	${nodeIndex}
	collapseTreeNode	${treeName}	${nodeIndex}
	treeNodeShouldBeCollapsed	${treeName}	${nodeIndex}
	collapseTreeNode	${treeName}	${nodeIndex}
	collapseTreeNode	${treeName}	0

Collapse Node By Name
	expandTreeNode	${treeName}	${rootNode}
	treeNodeShouldBeExpanded	${treeName}	${rootNode}
	collapseTreeNode	${treeName}	${rootNode}
	treeNodeShouldBeCollapsed	${treeName}	${rootNode}
	collapseTreeNode	${treeName}	${rootNode}

Tree Node Should Be Collapsed
	collapseTreeNode	${treeName}	${rootNode}
	treeNodeShouldBeCollapsed	${treeName}	0
	treeNodeShouldBeCollapsed	${treeName}	${rootNode}

Tree Node Should Be Expanded
	expandTreeNode	${treeName}	${rootNode}
	treeNodeShouldBeExpanded	${treeName}	0
	treeNodeShouldBeExpanded	${treeName}	${rootNode}

Select Tree Node By Index
	clearTreeSelection	${treeName}
	selectTreeNode	${treeName}	0
	treeNodeShouldBeSelected	${treeName}	0
	unselectTreeNode	${treeName}	0

Select Tree Node By Name
	clearTreeSelection	${treeName}
	selectTreeNode	${treeName}	${rootNode}
	treeNodeShouldBeSelected	${treeName}	${rootNode}
	unselectTreeNode	${treeName}	${rootNode}

Unselect Tree Node By Index
	clearTreeSelection	${treeName}
	selectTreeNode	${treeName}	0
	unselectTreeNode	${treeName}	0
	treeNodeShouldNotBeSelected	${treeName}	0

Unselect Tree Node By name
	clearTreeSelection	${treeName}
	selectTreeNode	${treeName}	${rootNode}
	unselectTreeNode	${treeName}	${rootNode}
	treeNodeShouldNotBeSelected	${treeName}	${rootNode}

Tree Node Should Be Selected AND Tree Node Should Not Be Selected
	clearTreeSelection	${treeName}
	treeNodeShouldNotBeSelected	${treeName}	0
	treeNodeShouldNotBeSelected	${treeName}	${rootNode}
	selectTreeNode	${treeName}	0
	treeNodeShouldBeSelected	${treeName}	0
	treeNodeShouldBeSelected	${treeName}	${rootNode}

Select From Tree Node Popup Menu
	selectFromTreeNodePopupMenu	${treeName}	${rootNode}	Insert a child
	expandTreeNode	${treeName}	${rootNode}
	treeNodeShouldBeVisible	${treeName}	${rootNode}|child
	selectFromTreeNodePopupMenu	${treeName}	${rootNode}|child	Remove this node

Select From Tree Node Popup Menu Fails If Menu Item Is Disabled	[tags]	development
	runKeywordAndExpectError	Menu item 'Disabled menuitem' was disabled.	selectFromTreeNodePopupMenu	${treeName}	${rootNode}	Disabled menuitem

Select From Tree Node Popup Menu In Separate Thread
	selectFromTreeNodePopupMenuInSeparateThread	${treeName}	${rootNode}	Show dialog
	dialogShouldBeOpen	Message
	closeDialog	Message

Tree Node Should Be Visible AND Tree Node Should Not Be Visible
	treeNodeShouldBeVisible	${treeName}	${rootNode}
	expandTreeNode	${treeName}	${rootNode}
	treeNodeShouldBeVisible	${treeName}	${anotherNodePath}
	expandTreeNode	${treeName}	${anotherNodePath}
	treeNodeShouldBeVisible	${treeName}	${leafNodePath}
	collapseTreeNode	${treeName}	${rootNode}
	treeNodeShouldNotBeVisible	${treeName}	${anotherNodePath}
	treeNodeShouldNotBeVisible	${treeName}	${leafNodePath}

Tree Node Should Be Leaf AND Tree Node Should Not Be Leaf
	expandTreeNode	${treeName}	${rootNode}
	expandTreeNode	${treeName}	${anotherNodePath}
	treeNodeShouldBeLeaf	${treeName}	${leafNodePath}
	treeNodeShouldNotBeLeaf	${treeName}	${anotherNodePath}
	collapseTreeNode	${treeName}	${rootNode}
	treeNodeShouldNotBeLeaf	${treeName}	${rootNode}

Tree Node Should Exist By Name
	treeNodeShouldExist	${treeName}	${rootNode}
	treeNodeShouldExist	${treeName}	${anotherNodePath}
	treeNodeShouldExist	${treeName}	${leafNodePath}

Tree Node Should Exist By Index
	treeNodeShouldExist	${treeName}	0

Tree Node Should Not Exist By Name
	treeNodeShouldNotExist	${treeName}	Unexisting|node|name
	treeNodeShouldNotExist	${treeName}	Unexisting_root_node_name

Tree Node Should Not Exist By Index
	treeNodeShouldNotExist	${treeName}	9999

Test Selecting Multiple Nodes
	expandTreeNode	${treeName}	${rootNode}
	clearTreeSelection	${treeName}
	selectTreeNode	${treeName}	${rootNode}
	selectTreeNode	${treeName}	${anotherNodePath}
	treeNodeShouldBeSelected	${treeName}	${rootNode}
	treeNodeShouldBeSelected	${treeName}	${anotherNodePath}
	collapseTreeNode	${treeName}	${rootNode}
	clearTreeSelection	${treeName}

Tree Node Popup Menu Item Should Be Enabled
	treeNodePopupMenuItemShouldBeEnabled	${rootNode}	Insert a child
	treeNodePopupMenuItemShouldBeEnabled	${rootNode}	Submenu|Enabled menuitem
	popupMenuShouldNotBeDisplayed

Tree Node Popup Menu Item Should Be Disabled
	treeNodePopupMenuItemShouldBeDisabled	${treeName}	${rootNode}	Disabled menuitem
	treeNodePopupMenuItemShouldBeDisabled	${treeName}	${rootNode}	Submenu|Disabled menuitem
	popupMenuShouldNotBeDisplayed

Tree Node Popup Menu Item Should Be Disabled Should Fail If Menu Item Is Enabled
	runKeywordAndExpectError	*Menu item 'Insert a child' was enabled*	treeNodePopupMenuItemShouldBeDisabled	${treeName}	${rootNode}	Insert a child

Tree Node Popup Menu Item Should Be Enabled Should Fail If Menu Item Is Disabled
	runKeywordAndExpectError	*Menu item 'Disabled menuitem' was disabled*	treeNodePopupMenuItemShouldBeEnabled	${treeName}	${rootNode}	Disabled menuitem

Keywords Also Work With Invisible Root
	expandTreeNode	${treeName}	${rootNode}
	selectFromTreeNodePopupMenu	${treeName}	${rootNode}	Hide root node
	treeNodeShouldBeVisible	${treeName}	${childNode1}
	treeNodeShouldBeVisible	${treeName}	${childNode2}
	treeNodeShouldExist	${treeName}	${childNode1}
	treeNodeShouldExist	${treeName}	${childNode2}
	treeNodeShouldExist	${treeName}	${childNode2}|${leafNode}
	expandTreeNode	${treeName}	${childNode2}
	treeNodeShouldBeVisible	${treeName}	${childNode2}|${leafNode}
	collapseTreeNode	${treeName}	${childNode2}
	[Teardown]	selectFromTreeNodePopupMenu	${treeName}	0	Show root node

Tree Keywords Fail If Context Is Not a tree
	selectEmptyContext
	keywordShouldFailBecauseContextIsIllegal	clearTreeSelection
	keywordShouldFailBecauseContextIsIllegal	expandTreeNode	Path|To|Tree|Node
	keywordShouldFailBecauseContextIsIllegal	collapseTreeNode	Path|To|Tree|Node
	keywordShouldFailBecauseContextIsIllegal	treeNodeShouldBeExpanded	Path|To|Tree|Node
	keywordShouldFailBecauseContextIsIllegal	treeNodeShouldBeCollapsed	Path|To|Tree|Node
	keywordShouldFailBecauseContextIsIllegal	selectFromTreeNodePopupMenu	Path|To|Tree|Node	SomethingInMenu
	keywordShouldFailBecauseContextIsIllegal	selectFromTreeNodePopupMenuInSeparateThread	Path|To|Tree|Node	SomethingInMenu
	keywordShouldFailBecauseContextIsIllegal	selectTreeNode	Path|To|Tree|Node
	keywordShouldFailBecauseContextIsIllegal	unselectTreeNode	Path|To|Tree|Node
	keywordShouldFailBecauseContextIsIllegal	treeNodeShouldBeSelected	Path|To|Tree|Node
	keywordShouldFailBecauseContextIsIllegal	treeNodeShouldNotBeSelected	Path|To|Tree|Node
	keywordShouldFailBecauseContextIsIllegal	treeNodeShouldBeVisible	Path|To|Tree|Node
	keywordShouldFailBecauseContextIsIllegal	treeNodeShouldNotBeVisible	Path|To|Tree|Node
	keywordShouldFailBecauseContextIsIllegal	treeNodeShouldBeLeaf	Path|To|Tree|Node
	keywordShouldFailBecauseContextIsIllegal	treeNodeShouldNotBeLeaf	Path|To|Tree|Node
	keywordShouldFailBecauseContextIsIllegal	treeNodeShouldExist	Path|To|Tree|Node
	keywordShouldFailBecauseContextIsIllegal	treeNodeShouldNotExist	Path|To|Tree|Node
	keywordShouldFailBecauseContextIsIllegal	treeNodePopupMenuItemShouldBeDisabled	Path|To|Tree|Node	SomethingInMenu
	keywordShouldFailBecauseContextIsIllegal	treeNodePopupMenuItemShouldBeDisabled	Path|To|Tree|Node	SomethingInMenu
	[teardown]	selectMainWindow

*Keyword*	*Action*	*Argument*	*Argument*
keywordShouldFailBecauseContextIsIllegal	[arguments]	${keyword}	@{arguments}
	runKeywordAndExpectError	*To use this keyword you must first select a correct context*	${keyword}	@{arguments}

popupMenuShouldNotBeDisplayed
	componentShouldNotExist	popupMenu

